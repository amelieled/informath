{-# LANGUAGE GADTs, KindSignatures, DataKinds #-}
{-# LANGUAGE LambdaCase #-}

{-| This file defines top-level conversions between different formats.
The main directions are from Dedukti to Agda, Lean, and Rocq on the formal side
and English, French, German, and Swedish on the natural language side.
Also translations directly between natural languages are possible.
-}

module InformathAPI where

import Dedukti.PrintDedukti
import Dedukti.ParDedukti
import Dedukti.AbsDedukti
import Dedukti.ErrM
import DeduktiOperations
import ParseInformath (parseJmt, unindexGFTree)
import Lexing
import qualified Dedukti2Agda as DA
import qualified Dedukti2Rocq as DR
import qualified Dedukti2Lean as DL

import Ranking
import Environment
import BuildConstantTable
import AnnotateDedukti (harmonizeJmt, annotateDkIdents, DkTree)
import qualified Dedukti2MathCore as DMC
import qualified MathCore2Informath as MCI
import qualified Informath2MathCore as IMC
import qualified MathCore2Dedukti as MCD
import Utils

import ProofText(proofDemo)

import Informath
import PGF

import Data.List (partition, isSuffixOf, isPrefixOf, isInfixOf, intersperse, sortOn, nub)
import Data.Char (isDigit, toUpper)
import qualified Data.Map as M
import qualified Data.Set as S
import Text.JSON (JSValue)
import System.Environment(getEnv)


-- * The environment

{- |
The environment, of type Env, is a large record of data and methods that affect
the conversions and the way they are displayed. It is defined in the module Environment,
which is not exported by the API. Most of the functions here presuppose an Env, which
is read using a list of flags. This list can empty, which results in a default environment.

Flags are strings of the forms -<option> of -<flag>=<value>.
The actual flags can be seen with RunInformath -help.
The same flags can be written in the [Flag] list when calling readEnv as an API function.
-}

informathRootVar :: String
informathRootVar = "INFORMATH_ROOT"

readEnv :: [Flag] -> IO Env
readEnv args = do
  root <- getEnv informathRootVar
  mo <- readDeduktiModule (argValues "-base" (root ++ "/" ++ baseConstantFile) args)
  gr <- readGFGrammar (argValue "-grammar" (root ++ "/" ++ grammarFile args) args)
  let symboltables =
        if flagHasValue "-add-symboltables" args
        then (root ++ "/" ++ constantTableFile) : argValues "-add-symboltables" "" args
        else argValues "-symboltables" (root ++ "/" ++ constantTableFile) args
  let fro = mkLanguage gr (argValue "-from-lang" english args)
  let sym = mkLanguage gr (argValue "-symboltable-lang" english args)
  symt <- readSymbolTable gr sym symboltables
  ifArg "-check-constant-table" args (unlines (checkSymbolTable mo gr symt))
  return Env {
    flags = args,
    informathRoot = root,
    grammar = gr,
    symbolTable = symt,
    reachableFunctions = reachableGFFunctions (backConstantTable symt),
    baseConstantModule = mo,
    formalisms = words "agda dedukti lean rocq",
    langs = languages gr, ---- | relevantLanguages gr args,
    toLang = mkLanguage gr (argValue "-to-lang" english args),
    toFormalism = argValue "-to-formalism" "NONE" args,
    fromLang = fro,
    symbolTableLang = sym,
    nbestNLG = argValueMaybeInt "-nbest" args,
    scoreWeights = commaSepInts (argValue "-weights" "1,1,1,1,1,1,1,1,1" args),
    samplingFactor = read (argValue "-sampling" "2" args), 
    morpho = buildMorpho gr fro
    }

-- | direct access to parts of the symbol table

constantTableEnv = constantTable . symbolTable
backConstantTableEnv = backConstantTable . symbolTable
conversionTableEnv = conversionTable . symbolTable
dropTableEnv = dropTable . symbolTable
macroTableEnv = macroTable . symbolTable
semanticsTableEnv = semanticsTable . symbolTable
nlgTableEnv = nlgTable . symbolTable
builtinSetEnv = builtinSet . symbolTable

-- ** Low-level access to data sources

{- | The most important data sources are a GF grammar (file .pgf)
and a ConstantTable (file .dkgf).
Both of these can be customized and passed as values of flags.
The following functions read them directly, but need hardly be called explicitly.
-}

-- | To read the GF grammar from a .pgf file.    
readGFGrammar :: FilePath -> IO PGF
readGFGrammar = readPGF

-- | To read a symbol table from a .dkgf file. 
readSymbolTable :: PGF -> Language -> [FilePath] -> IO SymbolTable
readSymbolTable pgf lang dkgfs = do
  ls <- mapM readFile dkgfs >>= return . concatMap lines 
  return $ buildSymbolTable pgf lang ls

-- | To construct a concrete syntax name from a 3-letter language code.
mkLanguage :: PGF -> String -> Language
mkLanguage pgf code = case readLanguage (informathPrefix ++ code) of
  Just lang | elem lang (languages pgf) -> lang
  _ -> error ("not a valid language: " ++ code)

-- * Default source files, which can be changed in Env flags (see RunInformath -help)

engGrammarFile = "share/InformathEng.pgf" 
fullGrammarFile = "share/InformathFull.pgf" 
baseConstantFile = "share/baseconstants.dk"  
constantTableFile = "share/baseconstants.dkgf"

-- * select English-only (default) or full grammar (if -to-lang or -for-lang is other than Eng)
-- * can be overridden with the -grammar=<file>.pgf flag
grammarFile :: [Flag] -> FilePath
grammarFile args = case (argValue "-from-lang" english args,
                         argValue "-to-lang" english args,
                         argValue "-symboltable-lang" english args) of
  ("Eng", "Eng", "Eng") -> engGrammarFile
  _ -> fullGrammarFile

-- * Main types involved

-- GF abstract syntax tree: from BuildConstantTable
-- type GFTree = Expr

-- | Dedukti judgement
type DkJmt = Jmt   

-- * Main conversion steps

-- | The whole line of generation from Dedukti to formal and natural languages.

processDeduktiModule :: Env -> Module -> [GenResult]
processDeduktiModule env (MJmts jmts) = map (processJmt env) jmts

-- | The whole line of parsing latex code and converting to Dedukti.
-- This assumes that parsing units are single lines not starting with a backslash \ or % 

processLatex :: Env -> String -> [ParseResult]
processLatex env = map (processLatexLine env) . filter parsable . map uncomment . lines
 where
   parsable line = not (null (words line))
   uncomment line = case line of
     c:d:cs | c == '\\' -> c : d : uncomment cs -- preserve \% 
     '%' : cs -> uncomment cs
     c : cs -> c : uncomment cs
     _ -> line


-- * Conversion outcomes

-- | The result of conversion from Dedukti, with intermediate phases available for debugging.

data GenResult = GenResult {
  originalDedukti  :: Jmt,
  annotatedDedukti :: [Jmt],
  coreGF           :: [GFTree],
  nlgResults       :: [(Language, [((GFTree, String), (Scores, Int))])],
  backToDedukti    :: [Jmt]  --- | for debugging NLG and semantics
  }

-- | The result of conversion from informal Latex text, with intermediate phases for debugging.
---- | TODO: complete formalResults with type checking in Dedukt.

data ParseResult = ParseResult {
  originalLine  :: String,
  lexedLine     :: String,
  termIndex     :: [String],
  indexedLine   :: String,
  parseMessage  :: String,
  unknownWords  :: [String],
  parseResults  :: [GFTree],
  unindexedResults :: [GFTree],
  formalResults :: [(GFTree, GFTree, GFTree, [Jmt])], -- | parsed, unindexed, normalized
  transResults  :: [String] -- | back-translation or translation to another language
  }


-- * The conversions in more detail

-- | Processing a single Dedukti judgement.

processJmt :: Env -> Jmt -> GenResult
processJmt env djmt =
  if toFormalism env /= "NONE"
  then dummyGenResult (applyDeduktiConversions env djmt)
  else
    let
      jmts = annotateDedukti env (applyDeduktiConversions env djmt)
      core = map dedukti2core jmts
      exts = concatMap (core2ext env) core
      nlgs = setnub $ map gf $ exts
      vars = if elem "-variations" (flags env) then id else (take 1) 
      best = maybe vars take (nbestNLG env)
      nlglins lang = [(tree, unlex env (gftree2nat env lang tree)) | tree <- nlgs]
      nlgranks = [(lang, best (rankGFTreesAndNat env (nlglins lang))) | lang <- langs env]
    in GenResult {
      originalDedukti = djmt,
      annotatedDedukti = jmts,
      coreGF = map gf core,
      nlgResults = nlgranks,
      backToDedukti = setnub (concatMap (gjmt2dedukti env) exts)
      }

-- | When just converting form Dk to another formalism, no GF is needed.
dummyGenResult :: Jmt -> GenResult
dummyGenResult jmt = GenResult jmt [jmt] undefined [] []


-- | Get GenResult from a GF tree instead of a Dedukti Jmt

processGFTree :: Env -> GFTree -> GenResult
processGFTree env gft =
    let
      jmts = []
      core = [gft]
      exts = concatMap (core2ext env . fg) core
      nlgs = setnub $ map gf $ exts
      vars = if elem "-variations" (flags env) then id else (take 1) 
      best = maybe vars take (nbestNLG env)
      nlglins lang = [(tree, unlex env (gftree2nat env lang tree)) | tree <- nlgs]
      nlgranks = [(lang, best (rankGFTreesAndNat env (nlglins lang))) | lang <- langs env]
      backs = setnub (concatMap (gjmt2dedukti env) exts)
    in GenResult {
      originalDedukti = head (gjmt2dedukti env (fg gft)),
      annotatedDedukti = jmts,
      coreGF = core,
      nlgResults = nlgranks,
      backToDedukti = backs
      }


-- | Processing a single line of LaTeX.

processLatexLine :: Env -> String -> ParseResult
processLatexLine env s =
  let
    gr = grammar env
    trans = isFlag "-translate" env
    parseonly = isFlag "-parse-only" env
    ls = lextex s
    (ils, tindex) = indexTex ls
    Just jmt = readType "Jmt"
    (mts, msg) = parseJmt env jmt ils
    ts = maybe [] id mts
    uts = map (unindexGFTree env tindex) ts
  in ParseResult {
    originalLine = s,
    lexedLine = ls,
    termIndex = tindex,
    indexedLine = ils,
    parseMessage = msg,
    unknownWords = missingWords env ils,
    parseResults = ts,
    unindexedResults = uts,
    formalResults =
      if trans || parseonly
      then []
      else [
        (t, ut, gf ct, gjmt2dedukti env ct) |
          t <- ts,
          ut <- uts,
          let fut = tracs env ("FUT.") (fg ut),
          ct <- ext2core env fut
          ],
    transResults = [
      unindexString tindex
        (unlex env (gftree2nat env (toLang env) t)) | t <- ts]
    }


-- * Dedukti-internal onversions

-- | Selected by flags in the environment, see -help.

applyDeduktiConversions :: Env -> DkTree a -> DkTree a
applyDeduktiConversions env t = foldl (flip ($)) t fs where
  fs :: [DkTree a -> DkTree a]
  fs = [f | (flag, f) <- [
          ("-peano2int", peano2int),
          ("-drop-qualifs", stripQualifiers),
          ("-drop-definitions", dropDefinitions),
          ("-hide-arguments", ignoreFirstArguments (dropTableEnv env))
         ], isFlag flag env
       ]

-- * Phases of the conversion pipeline

-- | Annotate Dedukti with GF information.
annotateDedukti :: Env -> Jmt -> [Jmt]
annotateDedukti env t = annotateDkIdents msyns msymbs (constantTableEnv env) (dropTableEnv env) (harmonizeJmt t)
  where
    msyns = argValueMaybeInt "-synonyms" (flags env)
    msymbs = argValueMaybeInt "-symbolics" (flags env)

-- | From annotated Dedukti to MathCore.
dedukti2core :: Jmt -> GJmt
dedukti2core = DMC.jmt2core

-- | Check type mismatches and missing items; performed as a part of building Env
checkSymbolTable :: Module -> PGF -> SymbolTable -> [String]
checkSymbolTable = symbolTableErrors

printSymbolTable :: SymbolTable -> String ---- TODO show complete information
printSymbolTable st = unlines $ [
  showConstantTable (constantTable st),
  "# semantics table keys: " ++ show (M.keys (semanticsTable st)),
  "# NLG table keys: " ++ show (M.keys (nlgTable st))
  ] ++
  macroCommands (macroTable st)

printSymbolTableLong :: SymbolTable -> String
printSymbolTableLong = showConstantTableLong . constantTable


-- * Printing conversions

-- ** Conversions starting from Dedukti

-- | With or without intermediate phases, as selected by flags in Env.
printGenResult :: Env -> GenResult -> [String]
printGenResult env result = case 0 of
  _ | toFormalism env /= "NONE" ->
    [printFormalismJmt env (toFormalism env) (originalDedukti result)]
  _ | isFlag "-json" env || any (flip isFlag env) ["-v", "-vs"] -> [showJsonGenResult env result]
  _ | isFlag "-parallel-data" env -> [showParallelData env result] 
  _ -> printNLGOutput env result


-- | Just the final NLG results.
printNLGOutput :: Env -> GenResult -> [String]
printNLGOutput env result = case (lookup (toLang env) (nlgResults result)) of
  Just phrases -> map (snd . fst) phrases
  _ -> error $ "language not available: " ++ (showCId (toLang env)) ++
               ". Available values: " ++ unwords (map showCId (langs env))

showJsonGenResult :: Env -> GenResult -> String
showJsonGenResult env result = encodeJSON $ mkJSONObject $ [
    mkJSONField "originalDedukti" (stringJSON (printDeduktiEnv env (originalDedukti result))),
    mkJSONListField "annotatedDedukti" (map (stringJSON . printDeduktiEnv env) (annotatedDedukti result)),
    mkJSONListField "coreGF" (map (stringJSON . showExpr []) (coreGF result))] ++
    if isFlag "-vs" env then [] else [
    mkJSONField "nlgResults" (mkJSONObject [
      mkJSONListField (showCId lang) (map (printRank) ranks) | (lang, ranks) <- nlgResults result]),
    mkJSONListField "backToDedukti" [stringJSON (printDeduktiEnv env jmt) | jmt <- backToDedukti result]
    ]

-- | The scores for each tree an string, in JSON.
printRank :: ((GFTree, String), (Scores, Int)) -> JSValue
printRank ((tree, str), (scores, rank)) = mkJSONObject [
  mkJSONField "tree" (stringJSON (showExpr [] tree)),
  mkJSONField "lin" (stringJSON str),
  mkJSONField "scores" (stringJSON (show scores)),
  mkJSONField "penalty" (stringJSON (show rank))
  ]

-- | Parallel data, usable for extracting pairs for trainingan an LLN.
showParallelData :: Env -> GenResult -> String
showParallelData env result = encodeJSON $ mkJSONObject $ [
    mkJSONField formalism
      (stringJSON (printFormalismJmt env formalism (originalDedukti result)))
        | formalism <- formalisms env
  ] ++ [  
    mkJSONListField (showCId lang) (map (stringJSON . snd . fst) ranks)
      | (lang, ranks) <- nlgResults result
  ]

-- | Converting Dedukti code to different formalisms.
printFormalismJmt :: Env -> String -> Jmt -> String
printFormalismJmt env formalism jmt = case formalism of
  "agda" -> dedukti2agda env jmt
  "lean" -> dedukti2lean env jmt
  "rocq" -> dedukti2rocq env jmt
  "dedukti" -> printDeduktiEnv env jmt
  f -> error $ "formalism not available: " ++ f ++ ". Available values: " ++ unwords (formalisms env)


-- | These are syntactic conversions, therefore total but may fail to typecheck in the targets.
-- | Notice: imports may have to be added to the generated files.

dedukti2agda :: Env -> Jmt -> String
dedukti2agda env jmt = unlines [DA.printAgdaJmts (DA.transJmt (conv jmt))] where
  conv = maybe id alphaConvert (M.lookup "agda" (conversionTableEnv env))

dedukti2lean :: Env -> Jmt -> String
dedukti2lean env jmt = unlines [DL.printLeanJmt (DL.transJmt (conv jmt))] where
  conv = maybe id alphaConvert (M.lookup "lean" (conversionTableEnv env))

dedukti2rocq :: Env -> Jmt -> String
dedukti2rocq env jmt = unlines [DR.printRocqJmt (DR.transJmt (conv jmt))] where
  conv = maybe id alphaConvert (M.lookup "rocq" (conversionTableEnv env))

-- | TODO: type-check a Dedukti judgement.
checkJmt :: Jmt -> Bool
checkJmt jmt = True ----

-- ** Conversions starting from natural language

-- | Print the parse results with all intermediate phases.
printParseResult :: Env -> ParseResult -> [String]
printParseResult env result = case 0 of
  _ | toFormalism env /= "NONE" ->
    [printFormalismJmt env (toFormalism env) jmt | (_,_,_,jmts) <- formalResults result, jmt <- nub jmts]
  _ | isFlag "-translate" env ->
    transResults result
  _ | isFlag "-parse-only" env ->
    map (showExpr []) (unindexedResults result)
  _ | isFlag "-failures" env ->
    if null (parseResults (result))
    then [originalLine result]
    else []
  _ | isFlag "-results" env ->
    if null (parseResults (result))
    then ["FAILURE: " ++ originalLine result]
    else ["SUCCESS: " ++ originalLine result]
  _ | isFlag "-json" env || isFlag "-v" env -> [encodeJSON $ mkJSONObject [
    mkJSONField "originalLine" (stringJSON (originalLine result)),
    mkJSONField "lexedLine" (stringJSON (lexedLine result)),
    mkJSONListField "termIndex" (map stringJSON (termIndex result)),
    mkJSONField "indexedLine" (stringJSON (indexedLine result)),
    mkJSONField "parseMessage" (stringJSON (parseMessage result)),
    mkJSONListField "unknownWords" (map stringJSON (unknownWords result)),
    mkJSONListField "formalResults" (map (finalParseResult env) (formalResults result))
    ]]
  _ -> printDeduktiOutput env result

-- | Print just the resulting Dedukti code.
printDeduktiOutput :: Env -> ParseResult -> [String]
printDeduktiOutput env result =
  nub [printDeduktiEnv env jmt | (_,_,_,jmts) <- formalResults result, jmt <- jmts]

-- | Print both GF trees and resulting Dedukti, in JSON. 
finalParseResult :: Env -> (GFTree, GFTree, GFTree, [Jmt]) -> JSValue
finalParseResult env (t, ut, ct, jmts) = mkJSONObject [
  mkJSONField "parseTree" (stringJSON (showExpr [] t)),
  mkJSONField "unindexedTree" (stringJSON (showExpr [] ut)),
  mkJSONField "coreTree" (stringJSON (showExpr [] ct)),
  mkJSONListField "dedukti" (map (stringJSON . printDeduktiEnv env) jmts)
  ]


-- ** General printing facilities

-- | Results are printed line by line, or with a Latex preamble in a document environment. 
printResults :: Env -> [String] -> [String]
printResults env ss = 
  if isFlag "-to-latex-doc" env
  then toLatexDoc (macroCommands (macroTableEnv env)) (intersperse "" (nub ss))
  else ss

-- | To print Dedukti under environment options, given in flags. 
printDeduktiEnv :: Print a => Env -> a -> String
printDeduktiEnv env t =
  if (isFlag "-dedukti-tokens" env)
  then unwords (deduktiTokens (printTree t))
  else printTree t

-- | to translate 
transEmbeddedDedukti :: Env -> [String] -> [String]
transEmbeddedDedukti env = transInEnv "dedukti" transDkEnv where
  transDkEnv (beg : ls) = trans (unlines (init ls))
  trans = unlines . intersperse "" . concatMap (printGenResult env) . processDeduktiModule env .  parseDeduktiModule

-- ** Seldom explicitly needed one-step conversion.

core2ext :: Env -> GJmt -> [GJmt]
core2ext env jmt = MCI.nlg env jmt

rankGFTreesAndNat :: Env -> [(Expr, String)] -> [((Expr, String), (Scores, Int))]
rankGFTreesAndNat = rankTreesAndStrings

ext2core :: Env -> GJmt -> [GJmt]
ext2core env = IMC.semantics (semanticsTableEnv env)

gjmt2dedukti :: Env -> GJmt -> [Jmt] 
gjmt2dedukti env = concatMap (MCD.jmt2dedukti (backConstantTableEnv env) (dropTableEnv env)) . ext2core env

core2dedukti :: Env -> GJmt -> [Jmt]
core2dedukti env = MCD.jmt2dedukti (backConstantTableEnv env) (dropTableEnv env)


-- * Reading input for processing

-- | Tp read a Dedukti module from one or more files; in translations, only from one file.
readDeduktiModule :: [FilePath] -> IO Module
readDeduktiModule files = mapM readFile files >>= return . parseDeduktiModule . unlines

-- | To parse a Dedukti file into its AST.
parseDeduktiModule :: String -> Module
parseDeduktiModule s = case pModule (myLexer s) of
  Bad e -> error ("parse error: " ++ e)
  Ok mo -> mo

-- | To parse a Dedukti file into its AST.
parseDeduktiModuleErrorFree :: String -> Maybe Module
parseDeduktiModuleErrorFree s = case pModule (myLexer s) of
  Bad e -> Nothing
  Ok mo -> return mo

-- | To linearize a GF tree.
gftree2nat :: Env -> Language -> GFTree -> String
gftree2nat env lang tree = linearize (grammar env) lang tree

-- | To unlex in a latex-like style, overridded by flag -no-unlex.
unlex :: Env -> String -> String
unlex env s = if (isFlag "-no-unlex" env) then s else unlextex s

-- * Statistics about data.

-- | Statistics of unknown identifiers in Dedukti (not listed in .dkgf)
identsInDedukti :: Env -> Module -> [(String, Int)]
identsInDedukti env mo = map stringify (mfilter freqs) where
  stringify (c, i) = (printDeduktiEnv env c, i)
  freqs = sortOn (\ (_,i) -> -i) (M.toList (identsInTypes mo))
  mfilter = if elem "-unknown-idents" (flags env) then filter (notInTable . fst) else id 
  notInTable qid = M.notMember qid (constantTableEnv env) && not (all isDigit (printDeduktiEnv env qid))


-- | Statistics of unknown words in text (not recognized in .pgf).
unknownWordsInTex :: Env -> String -> [(String, Int)]
unknownWordsInTex env = frequencyTable . missingWords env . lextex

-- | List of missing words on a line.
missingWords :: Env -> String -> [String]
missingWords env = morphoMissing (morpho env) . tokens . lextex
 where
   tokens = ordinary [] . words
   ordinary acc ws = case ws of
     "$" : ww -> case break (=="$") ww of (_, _:ww2) -> ordinary acc ww2 ; _ -> acc
     "$$" : ww -> case break (=="$$") ww of (_, _:ww2) -> ordinary acc ww2 ; _ -> acc
     w : ww -> ordinary (w:acc) ww
     _ -> acc

-- | show candidate GF functions that linearize to a given word
findGFFunctions :: Env -> String -> (String, [String])
findGFFunctions env w = (w, nub [showCId f | (f, _) <- lookupMorpho (morpho env) w])

-- | To linearize a GF tree (given as string) to a string
readGFtree2nat :: Env -> String -> String
readGFtree2nat env s = linearize (grammar env) (toLang env) (readGFtree s)

-- | To read a GF tree from a string
readGFtree :: String -> GFTree
readGFtree s = case readExpr s of
  Just tree -> tree
  Nothing -> error ("ERROR: not a valid tree: " ++ s)

-- | To show all GF functions with their types, one per line
showGFFunctions :: Env -> [String]
showGFFunctions env = [unwords ([showCId f, ":", showType [] t, "\t"] ++ map showLang (langs f))
                         | f <- functions gr, Just t <- [functionType gr f]]
  where
    gr = grammar env
    misses = [(lang, S.fromList (missingLins gr lang)) | lang <- languages gr]
    langs f = [lang | lang <- languages gr, S.notMember f (maybe S.empty id (lookup lang misses))]
    showLang = drop 9 . showCId  -- InformathEng -> Eng


-- | To parse an example to a tree
parseFunExample :: Env -> String -> [String]
parseFunExample env s = map showFunProfile (parseExample (grammar env) (symbolTableLang env) s)

-- | lexical functions reachable from the current symbol table
reachableGFFunctions :: BackConstantTable -> S.Set CId
reachableGFFunctions bt = S.fromList [f | t <-  M.keys bt, f <- ids t] where
  ids t = case unApp t of
    Just (f, xs) -> f : concatMap ids xs
    _ -> []


-- | try parse a symbol table line by line (only annotations for the time being), report success or failure
tryParseSymbolTable :: Env -> [String] -> [String]
tryParseSymbolTable env ls = map tryParse ils
  where
    ils = zip [1..] ls
    tryParse (i, s) = case tryParseConstantTableEntry (grammar env) (fromLang env) s of
      Ok _ | isFlag "-keep-ok-entries" env -> s
      Ok _ -> unwords ["#line", show i, "OK:", s]
      Bad m -> unwords ["#line", show i, "BAD:", s, "ERROR:", m]

--- proof text demo, experimental
showProofDemo :: Env -> Module -> Module -> String
showProofDemo env base mo = proofDemo env base mo unit2nat
 where
   unit2nat :: GUnit -> String
   unit2nat u = gftree2nat env (toLang env) (gf (best u))

   best u = head [u | GUnitJmt u <- core2ext env (GUnitJmt u)]

