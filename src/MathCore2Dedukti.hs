{-# LANGUAGE GADTs, KindSignatures, DataKinds #-}
{-# LANGUAGE LambdaCase #-}

module MathCore2Dedukti where

import Dedukti.AbsDedukti
import Informath -- superset of MathCore
import CommonConcepts
import DeduktiOperations
import BuildConstantTable 
import PGF (showExpr, readExpr, showCId, mkApp, mkCId)
import Utils (unescapeUnderscores)

import Data.Char
import Data.List (intersperse, nub, partition, isInfixOf)
import qualified Data.Map as M


jmt2dedukti :: BackConstantTable -> DropTable -> GJmt -> [Jmt]
jmt2dedukti lb dt =
  map eliminateLocalDefinitions .
  map (restoreFirstArguments dt) .
  filterNoUndefined .
  applyLookBack lb .
  jmt2jmt

-- remove UNDEFINED trees (coming from the parser) if there are any good ones
filterNoUndefined :: [Jmt] -> [Jmt]
filterNoUndefined js = case partition hasUndefineds js of
  (_, []) -> js
  (_, njs) -> njs
 where
  hasUndefineds :: Dedukti.AbsDedukti.Tree a -> Bool
  hasUndefineds t = any (\ (QIdent c) -> isInfixOf "_UNDEFINED" c || isInfixOf "UNRESOLVED_" c) (identsInTree t)

-- this is where the GF identifier ambiguity is resolved
applyLookBack ::  BackConstantTable -> Dedukti.AbsDedukti.Tree a -> [Dedukti.AbsDedukti.Tree a]
applyLookBack mb t = case t of
  EApp fun arg -> case splitApp t of
    (EIdent qid@(QIdent s), args) -> case M.lookup (mkTree s) mb of
      Just fps -> [unappProfile p (foldl EApp (EIdent f) aargs) | (f, p) <- fps, aargs <- sequence (map (applyLookBack mb) args)]
      _ -> [foldl EApp (EIdent (mark qid)) aargs | aargs <- sequence (map (applyLookBack mb) args)]
    _ -> [EApp afun aarg | afun <- applyLookBack mb fun, aarg <- applyLookBack mb arg]
  QIdent s -> maybe [mark t] (map fst) $ M.lookup (mkTree s) mb
  _ -> Dedukti.AbsDedukti.composOpM (applyLookBack mb) t  
 where
  mkTree s = case readExpr s of
    Just t -> t
    _ -> mkApp (mkCId (unescape s)) []
  unescape s = case s of
    _ -> s
  mark :: QIdent -> QIdent
  mark t@(QIdent s) = if isVar s then t else QIdent ("_UNDEFINED" ++ s)
  isVar s = length (words s) == 1 --- approximative matching of variable symbols 

jmt2jmt :: GJmt -> Jmt
jmt2jmt jment = case jment of
  GAxiomJmt label (GListHypo hypos) prop ->
    JStatic
      (label2ident label)
      (foldr EFun (prop2dedukti prop) (concatMap hypo2dedukti hypos))
  GThmJmt label (GListHypo hypos) prop proof ->
    JDef
      (label2ident label)
      (MTExp (foldr EFun (prop2dedukti prop) (concatMap hypo2dedukti hypos)))
      (MEExp (proof2dedukti proof))
  GDefPropJmt label_ (GListHypo hypos) prop df ->
    JDef
      (prop2deduktiIdent prop)
      (MTExp (foldr EFun typeProp (concatMap hypo2dedukti hypos)))
      (MEExp (prop2dedukti df))
  GDefKindJmt label_ (GListHypo hypos) kind df ->
    JDef
      (kind2ident kind)
      (MTExp (foldr EFun typeType (concatMap hypo2dedukti hypos)))
      (MEExp (kind2dedukti df))
  GDefExpJmt label_ (GListHypo hypos) exp kind df ->
    JDef
      (exp2ident exp)
      (MTExp (foldr EFun (kind2dedukti kind) (concatMap hypo2dedukti hypos)))
      (MEExp (exp2dedukti df))
  GAxiomPropJmt label_ (GListHypo hypos) prop ->
    JStatic
      (prop2deduktiIdent prop)
      (foldr EFun typeProp (concatMap hypo2dedukti hypos))
  GAxiomKindJmt label_ (GListHypo hypos) kind ->
    JStatic
      (kind2ident kind)
      (foldr EFun typeType (concatMap hypo2dedukti hypos))
  GAxiomExpJmt label_ (GListHypo hypos) exp kind ->
    JStatic
      (exp2ident exp)
      (foldr EFun (kind2dedukti kind) (concatMap hypo2dedukti hypos))
  GRewriteJmt (GListRule rules) -> JRules (map rule2dedukti rules)
  GDefUntypedExpJmt label_ exp df ->
    JDef
      (exp2ident exp)
      MTNone
      (MEExp (exp2dedukti df))
  GUnitJmt unit ->
    JDef
      (QIdent "unit")
      MTNone
      (MEExp (unit2exp unit))
  _ -> error ("TODO jmt2jmt: " ++ showGF jment)

rule2dedukti :: GRule -> Rule
rule2dedukti rule = case rule of
  GRewriteRule (GListIdent idents) patt exp ->
    RRule (map (PBVar . ident2ident) idents) (exp2deduktiPatt patt) (exp2dedukti exp)
  GNoVarRewriteRule patt exp ->
    rule2dedukti (GRewriteRule (GListIdent []) patt exp)


unit2exp :: GUnit -> Exp
unit2exp unit = case unit of
  GBeginEnvironmentUnit (LexEnvironment s) label -> wrap "BeginEnvironmentUnit" [EIdent (QIdent s)]
  GBeginProofMethodUnit label method_ -> wrap "BeginProofMethodUnit" [EIdent (label2ident label)]
  GCaseGoal prop ident -> wrap "CaseGoal" [prop2dedukti prop, EIdent (ident2ident ident)] 
  GCasesGoal -> uni "CasesGoal"
  GEndEnvironmentUnit (LexEnvironment s) -> wrap "EndAbbreviationUnit" [EIdent (QIdent s)] ;
  GEnoughGoal prop -> wrap "EnoughGoal" [prop2dedukti prop]
  GFirstVerifyGoal prop -> wrap "FirstVerifyGoal" [prop2dedukti prop]
  GFollowsPropConclusion prop -> wrap "FollowsPropConclusion" [prop2dedukti prop]
  GHyposAssumption (GListHypo hypos) -> wrap "HyposAssumptiop" [foldr EFun eUnit (concatMap hypo2dedukti hypos)]
  GIdentExpAssumption exp ident -> wrap "IdentExpAssumptiopn" [exp2dedukti exp, EIdent (ident2ident ident)]
  GIdentKindAssumption kind ident -> wrap "IdentKindAssumptiopn" [kind2dedukti kind, EIdent (ident2ident ident)]
  GInductionGoal -> uni "InductionGoal"
  GLabelConclusion label -> wrap "LabelConclution" [EIdent (label2ident label)]
  GObviousConclusion -> uni "ObviousConclusion"
  GPropAssumption prop label -> wrap "PropAssumption" [prop2dedukti prop, EIdent (label2ident label)]
  GPropConclusion hence prop -> wrap "PropConclusion" [prop2dedukti prop]
  GPropLabelConclusion hence prop label -> wrap "PropLabelConclusion" [prop2dedukti prop, EIdent (label2ident label)]
  GSinceConclusion a b -> wrap "SinceConclusion" [prop2dedukti a, prop2dedukti b]
  GSinceGoal a b -> wrap "SinceGoal" [prop2dedukti a, prop2dedukti b]
  _ -> eUnit
 where
  wrap s xs = foldl EApp (EIdent (QIdent s)) xs
  uni s = wrap s []
  eUnit = wrap "UNIT" []

prop2dedukti :: GProp -> Exp
prop2dedukti prop = case prop of
  GExistKindProp k -> kind2dedukti k
  GProofProp p -> EApp (EIdent identProof) (prop2dedukti p)
  GFalseProp -> propFalse
  GIdentProp ident -> EIdent (ident2ident ident)
  GCoreAndProp a b -> foldl1 propAnd (map prop2dedukti [a, b])
  GCoreOrProp a b -> foldl1 propOr (map prop2dedukti [a, b])
  GCoreIfProp a b -> propImp (prop2dedukti a) (prop2dedukti b)
  GCoreNotProp a -> propNot (prop2dedukti a)
  GCoreIffProp a b -> propEquiv (prop2dedukti a) (prop2dedukti b)
  GCoreAllProp kind ident prop ->
    propPi (kind2dedukti kind) (EAbs (BVar (ident2ident ident)) (prop2dedukti prop)) 
  GCoreExistProp kind ident prop ->
    propSigma (kind2dedukti kind) (EAbs (BVar (ident2ident ident)) (prop2dedukti prop)) 
  GAppProp ident exps ->
    foldl1 EApp ((EIdent (ident2ident ident)) : map exp2dedukti (exps2list exps))

  GAdjProp adj exp ->
    EApp (EIdent (QIdent (showGF adj))) (exp2dedukti exp)
  GAdj2Prop adj a b ->
    foldl EApp (EIdent (QIdent (showGF adj))) (map exp2dedukti [a, b])
  GAdjCProp adj a b ->
    foldl EApp (EIdent (QIdent (showGF adj))) (map exp2dedukti [a, b])
  GAdjEProp adj a b ->
    foldl EApp (EIdent (QIdent (showGF adj))) (map exp2dedukti [a, b])
  GAdj3Prop adj a b c ->
    foldl EApp (EIdent (QIdent (showGF adj))) (map exp2dedukti [a, b, c])

  GAdvProp adv exp ->
    EApp (EIdent (QIdent (showGF adv))) (exp2dedukti exp)
  GAdv2Prop adv a b ->
    foldl EApp (EIdent (QIdent (showGF adv))) (map exp2dedukti [a, b])
  GAdvCProp adv a b ->
    foldl EApp (EIdent (QIdent (showGF adv))) (map exp2dedukti [a, b])

  GVerbProp verb exp ->
    EApp (EIdent (QIdent (showGF verb))) (exp2dedukti exp)
  GVerb2Prop verb x y ->
    EApp (EApp (EIdent (QIdent (showGF verb))) (exp2dedukti x)) (exp2dedukti y)
  GVerbCProp verb x y ->
    EApp (EApp (EIdent (QIdent (showGF verb))) (exp2dedukti x)) (exp2dedukti y)
    
  GNoun1Prop noun exp ->
    EApp (EIdent (QIdent (showGF noun))) (exp2dedukti exp)
  GNoun2Prop noun x y ->
    EApp (EApp (EIdent (QIdent (showGF noun))) (exp2dedukti x)) (exp2dedukti y)
  GNounCProp noun x y ->
    EApp (EApp (EIdent (QIdent (showGF noun))) (exp2dedukti x)) (exp2dedukti y)
    
  GIndexedFormulaProp (GInt i) -> EIdent (unresolvedIndexIdent i)
  GFormulaProp formula -> formula2dedukti formula
  _ -> eUndefinedDebug prop ---- TODO complete Informath2Core

formula2dedukti :: GFormula -> Exp
formula2dedukti formula = case formula of
----  GElemFormula terms term ->
  GEquationFormula (GBinaryEquation (LexCompar compar) term1 term2) ->
    foldl EApp (EIdent (QIdent compar)) (map term2dedukti [term1, term2])
  ---- modulo_Formula : Term -> Term -> Term -> Formula
  GMacroFormula ident -> foldl EApp (EIdent (macro2ident ident)) []
  GApp1MacroFormula ident term -> foldl EApp (EIdent (macro2ident ident)) (map term2dedukti [term])
  GApp2MacroFormula ident x y -> foldl EApp (EIdent (macro2ident ident)) (map term2dedukti [x, y])
  GApp3MacroFormula ident x y z -> foldl EApp (EIdent (macro2ident ident)) (map term2dedukti [x, y, z])
  GApp4MacroFormula ident x y z u -> foldl EApp (EIdent (macro2ident ident)) (map term2dedukti [x, y, z, u])
  _ -> eUndefinedDebug formula ----

hypo2dedukti :: GHypo -> [Hypo]
hypo2dedukti hypo = case hypo of
  GVarsHypo (GListIdent idents) kind ->
    [HVarExp (ident2ident ident) (kind2dedukti kind) | ident <- idents]
  GPropVarHypo ident prop ->
    [HVarExp (ident2ident ident) (prop2dedukti prop)]
  GPropHypo prop ->
    [HExp (prop2dedukti prop)]
  GIndexedLetFormulaHypo (GInt i) ->
    [HExp (EIdent (unresolvedIndexIdent i))]
  GLocalHypo local ->
    [local2dedukti local]
  _ ->
    [HExp eUndefined]

local2dedukti :: GLocal -> Hypo
local2dedukti local = case local of
  GLetLocal ident kind exp ->
    HLetTyped (ident2ident ident) (kind2dedukti kind) (exp2dedukti exp)
  GBareLetLocal ident exp ->
    HLetExp (ident2ident ident) (exp2dedukti exp)

argkind2dedukti :: GArgKind -> [(Exp, QIdent)]
argkind2dedukti argkind = case argkind of
  GIdentArgKind kind ident ->
    let dkind = kind2dedukti kind
    in [(dkind, ident2ident ident)]
  GIdentsArgKind kind (GListIdent idents) ->
    let dkind = kind2dedukti kind
    in [(dkind, ident2ident ident) | ident <- idents]
  GIndexedDeclarationArgKind (GInt i) ->
    [(EIdent (unresolvedIndexIdent i), unresolvedIndexIdent i)]

kind2dedukti :: GKind -> Exp
kind2dedukti kind = case kind of
  GElemKind k -> EApp (EIdent identElem) (kind2dedukti k)

  GSuchThatKind kind ident prop ->
    foldl EApp (EIdent identSuchThat) [ 
      (kind2dedukti kind),
      (EAbs (BVar (ident2ident ident)) (prop2dedukti prop))]

  GFamKind fam exp ->
    EApp (EIdent (QIdent (showGF fam))) (kind2dedukti exp)
  GFam2Kind fam exp1 exp2 ->
    EApp (EApp (EIdent (QIdent (showGF fam))) (kind2dedukti exp1)) (kind2dedukti exp2)
  GDepKind fam exp ->
    EApp (EIdent (QIdent (showGF fam))) (exp2dedukti exp)
  GDep2Kind fam exp1 exp2 ->
    EApp (EApp (EIdent (QIdent (showGF fam))) (exp2dedukti exp1)) (exp2dedukti exp2)
  GDepCKind fam exp1 exp2 ->
    EApp (EApp (EIdent (QIdent (showGF fam))) (exp2dedukti exp1)) (exp2dedukti exp2)
  GExpKind exp -> exp2dedukti exp
  GNounKind noun ->
    EIdent (QIdent (showGF noun))
  _ -> eUndefinedDebug kind ---- TODO

exp2dedukti :: GExp -> Exp
exp2dedukti exp = case exp of
  GAppExp exp exps ->
    foldl1 EApp (map exp2dedukti (exp : (exps2list exps)))
  GAbsExp (GListIdent idents) exp ->
    foldr
      (\x y -> EAbs (BVar (ident2ident x)) y)
      (exp2dedukti exp)
      idents
  GNameExp (LexName name) ->
    EIdent (QIdent (name))
  GTermExp term -> term2dedukti term
  GFunExp f x -> appIdent (showGF f) (map exp2dedukti [x])

  GFun2Exp f x y -> appIdent (showGF f) (map exp2dedukti [x, y])
  GFunCExp f x y -> appIdent (showGF f) (map exp2dedukti [x, y])

  GBinderExp f i x -> appIdent (showGF f) [EAbs (BVar (ident2ident i)) (exp2dedukti x)]
  GBinder1Exp f k i x -> appIdent (showGF f) [kind2dedukti k, EAbs (BVar (ident2ident i)) (exp2dedukti x)]
  GBinder2Exp f low up i x -> appIdent (showGF f) [exp2dedukti low, exp2dedukti up, EAbs (BVar (ident2ident i)) (exp2dedukti x)]

  GIndexedTermExp (GInt i) -> EIdent (unresolvedIndexIdent i)
  GEnumSetExp exps -> EApp (EIdent identEnumset) (list2enum (map exp2dedukti (exps2list exps)))

  GKindExp kind -> kind2dedukti kind
  
  _ -> eUndefinedDebug exp ---- TODO

term2dedukti :: GTerm -> Exp
term2dedukti term = case term of
  GConstTerm (LexConst name) ->
    EIdent (QIdent (name))
  GOperTerm oper x ->
    appIdent (showGF oper) [term2dedukti x]
  GOper2Term oper x y ->
    appIdent (showGF oper) (map term2dedukti [x, y])
  GNumberTerm (GInt n) -> int2exp n
  GIdentTerm ident -> EIdent (ident2ident ident)
  GMacroTerm macro -> foldl EApp (EIdent (macro2ident macro)) []
  GApp1MacroTerm macro term -> foldl EApp (EIdent (macro2ident macro)) (map term2dedukti [term])
  GApp2MacroTerm macro x y -> foldl EApp (EIdent (macro2ident macro)) (map term2dedukti [x, y])
  GApp3MacroTerm macro x y z -> foldl EApp (EIdent (macro2ident macro)) (map term2dedukti [x, y, z])
  GComprehensionTerm kterm x pterm ->
    foldl EApp (EIdent identSuchThat) [ 
      (term2dedukti kterm),
      (EAbs (BVar (ident2ident x)) (formula2dedukti pterm))]
  GComprehensionTextTerm kterm x prop ->
    foldl EApp (EIdent identSuchThat) [ 
      (term2dedukti kterm),
      (EAbs (BVar (ident2ident x)) (prop2dedukti prop))]
  GAbsTerm ident body -> EAbs (BVar (ident2ident ident)) (term2dedukti body)
  GBetaRedexTerm ident body arg ->
    EApp (EAbs (BVar (ident2ident ident)) (term2dedukti body)) (term2dedukti arg)
  GFunTypeTerm arg val -> EFun (HExp (term2dedukti arg)) (term2dedukti val)

  _ -> eUndefinedDebug term ---- TODO

{-
termsList :: GTerms -> [GTerm]
termsList terms = case terms of
  GAddTerms t tt -> t : termsList terms
  GOneTerms t -> [t]
-}

exp2deduktiPatt :: GExp -> Patt
exp2deduktiPatt exp = case exp of
  GTermExp (GIdentTerm ident) -> PVar (ident2ident ident)
  GAppExp exp exps ->
    foldl1 PApp (map exp2deduktiPatt (exp : exps2list exps))
  GNameExp (LexName name) -> PVar (QIdent name)
{-
  GAbsExp (GListIdent idents) exp ->
    foldr
      (\x y -> EAbs (BVar (ident2ident x)) y)
      (exp2dedukti exp)
      idents
-}
  _ -> PVar (iUndefinedDebug exp) ---- TODO


proof2dedukti :: GProof -> Exp
proof2dedukti proof = case proof of
  GAppProof proofexp (GListProof proofs) ->
    foldl1 EApp (proofexp2exp proofexp : map proof2dedukti proofs)
----  GAbsProof hypos proof ->
----  GLabelProofExp label -> 

proofexp2exp :: GProofExp -> Exp
proofexp2exp proofexp = case proofexp of
  GLabelProofExp label -> EIdent (label2ident label)

ident2ident :: GIdent -> QIdent
ident2ident ident = case ident of
  GStrIdent (GString s) -> QIdent (unescapeUnderscores (escapeConstant s))

macro2ident :: GMacro -> QIdent
macro2ident ident = case ident of
  GStringMacro (GString s) -> QIdent ("'\\" ++ s ++ "'") ---- (escapeConstant s)

exp2ident :: GExp -> QIdent
exp2ident exp = case exp of
  GTermExp (GIdentTerm ident) -> ident2ident ident
  _ -> QIdent (takeWhile isAlpha (show (gf exp))) ---- TODO

label2ident :: GLabel -> QIdent
label2ident label = case label of
  LexLabel s -> QIdent (s)
  GIdentLabel ident -> ident2ident ident
  GcrefLabel ident -> ident2ident ident
  _ -> iUndefinedDebug label

kind2ident :: GKind -> QIdent
kind2ident kind = case kind of
  GExpKind (GTermExp (GIdentTerm ident)) -> ident2ident ident
  _ -> QIdent (takeWhile isAlpha (show (gf kind))) ---- TODO

prop2deduktiIdent :: GProp -> QIdent
prop2deduktiIdent prop = case prop of
  GIdentProp (GStrIdent (GString s)) -> QIdent s
  _ -> QIdent (takeWhile isAlpha (show (gf prop))) ---- TODO

eUndefined :: Exp
eUndefined = EIdent (QIdent "_UNDEFINED")

iUndefinedDebug :: Gf a => a -> QIdent
iUndefinedDebug t =
  QIdent (concat (intersperse "_" ("{|" : "UNDEFINED" : words (showExpr [] (gf t)))) ++ "|}")

eUndefinedDebug :: Gf a => a -> Exp
eUndefinedDebug t = EIdent (iUndefinedDebug t)

appIdent :: String -> [Exp] -> Exp
appIdent f exps = foldl EApp (EIdent (QIdent f)) exps

appQIdent :: QIdent -> [Exp] -> Exp
appQIdent f exps = foldl EApp (EIdent f) exps

showGF :: Gf a => a -> String
showGF = showGFTree . gf


--- also in MCI
exps2list :: GExps -> [GExp]
exps2list exps = case exps of
  GOneExps e -> [e]
  GManyExps (GListExp es) -> es

